//
//	DESERT 
//	Map #1
//	Mission #1
//
////////////////////////




//PAD DEFs

//Tan Base PADs
PAD BlueInside	15	BlueRifle1	> 0	//blue spy porch on target house
PAD TanInside	15	TanRifle1	> 0	//Tan guard porch on target house
PAD Inside		120				// target pad inside target house
//Base sentry points
PAD StationLR	11				//bottom right
PAD StationLL	12				//bottom left
PAD StationUL	13				//upper left
PAD StationUR	14				//upper right
//Base cover points
PAD Cover16		16				//left of outside bottom rock
PAD Cover17		17				//behind inside bottom rock
PAD Cover18		18				//behind inside upper rock circle
PAD Cover19		19				//West of Jeep
PAD Cover20		20				//South of Jeep
PAD Cover21		21				//East of Jeep
//Base regions
PAD Region0		0	GREEN > 0		//map 1 bottom right
PAD Region1		1	GREEN > 0		//map 2 bottom middle
PAD Region2		2	GREEN > 0		//map 4 bottom left
PAD Region3		3	GREEN > 0		//map 8 top right
PAD Region4		4	GREEN > 0		//map 16 top left
PAD Region5		5	GREEN > 0		//map 32 top above Jeep
PAD Region6		6	GREEN > 0		//map 64 sarge inside compound fence

PAD JeepDeath	50				//jeep explosion site


PAD AmmoTripLine	200	GreenSarge	> 0	//Ammo depot



// endgames
IF killed_GreenSarge THEN
	TRIGGERDELAY 1 tanwins

IF ALLOF killed_TanRifle10 on_AmmoTripLine THEN
	INVULNERABLESARGE,
	TRIGGERDELAY 1 greenwins

//INIT
IF startup THEN
	VULNERABLESARGE,
	ALLY TAN BLUE,
	ALLY GRAY GREEN,
	TRIGGER Orders,
	TRIGGER Frontline,
	SETAICONTROL UGreyRifle1,
	SETAICONTROL TanRifle21,	//guy behind wall by jeep
	ORDER TanRifle21 GOTO TanRifle21 INMODE ATTACK THEN ATMODE DEFEND,
	ORDER UGreyRifle1 GOTO GreenSarge INMODE ATTACK THEN ATMODE DEFEND,
	TRIGGERDELAY 5 GrayOrders


//bridge guards
IF Orders THEN
	SETAICONTROL TanRifle35,
	ORDER TanRifle35 GOTO GreenSarge INMODE ATTACK THEN ATMODE ATTACK,
	SETAICONTROL TanRifle36,
	ORDER TanRifle36 GOTO GreenSarge INMODE ATTACK THEN ATMODE ATTACK,
	DEPLOY TanRifle9 ATMODE STANDFIRE,
	DEPLOY TanRifle10 ATMODE STANDFIRE

IF GrayOrders THEN
	ORDER UGreyRifle1 GOTO AmmoTripLine INMODE ATTACK THEN ATMODE DEFEND

IF killed_UGreyRifle1 THEN
	TRIGGERDELAY 5 ResGray

IF ResGray THEN
	RESURRECT UGreyRifle1 Inside,
	SETAICONTROL UGreyRifle1,
	ORDER UGreyRifle1 GOTO AmmoTripLine INMODE ATTACK THEN ATMODE DEFEND

//stronghold stuff for mission 1 & 2
//resurrect guys if killed this mission
IF killed_TanRifle1 THEN
	TRIGGERDELAY 5 Res1
IF Res1 THEN
	RESURRECT TanRifle1 Inside,
	SETAICONTROL TanRifle1,
	ORDER TanRifle1 GOTO StationLR INMODE ATTACK THEN ATMODE DEFEND
IF killed_TanRifle2 THEN
	TRIGGERDELAY 5 Res2
IF Res2 THEN
	RESURRECT TanRifle2 Inside,
	SETAICONTROL TanRifle2,

	ORDER TanRifle2 GOTO StationLL INMODE ATTACK THEN ATMODE DEFEND
IF killed_TanRifle3 THEN
	TRIGGERDELAY 5 Res3
IF Res3 THEN
	RESURRECT TanRifle3 Inside,
	SETAICONTROL TanRifle3,
	ORDER TanRifle3 GOTO StationUL INMODE ATTACK THEN ATMODE DEFEND
IF killed_TanRifle4 THEN
	TRIGGERDELAY 5 Res4
IF Res4 THEN
	RESURRECT TanRifle4 Inside,
	SETAICONTROL TanRifle4,
	ORDER TanRifle4 GOTO StationUR INMODE ATTACK THEN ATMODE DEFEND

//Base Behaviors
IF on_Region0 THEN
	SETAICONTROL TanRifle1,
	ORDER TanRifle1 GOTO StationLR INMODE ATTACK THEN ATMODE DEFEND

IF ALLOF on_Region1 vison_GreenSarge THEN
	SETAICONTROL TanRifle2,
	ORDER TanRifle2 GOTO Cover16 INMODE IGNORE THEN ATMODE KNEELFIRE
IF off_Region1 THEN
	SETAICONTROL TanRifle2,
	ORDER TanRifle2 GOTO StationLL INMODE IGNORE THEN ATMODE DEFEND

IF ALLOF on_Region2 vison_GreenSarge THEN
	SETAICONTROL TanRifle2,
	ORDER TanRifle2 GOTO Cover17 INMODE IGNORE THEN ATMODE KNEELFIRE
IF off_Region2 THEN
	SETAICONTROL TanRifle2,
	ORDER TanRifle2 GOTO StationLL INMODE IGNORE THEN ATMODE DEFEND

IF ALLOF on_Region3 vison_GreenSarge THEN
	SETAICONTROL TanRifle4,
	ORDER TanRifle4 GOTO Cover18 INMODE IGNORE THEN ATMODE KNEELFIRE
IF ALLOF on_Region3 vison_GreenSarge THEN
	SETAICONTROL TanRifle3,
	ORDER TanRifle3 GOTO Cover19 INMODE IGNORE THEN ATMODE STANDFIRE
IF off_Region3 THEN
	SETAICONTROL TanRifle4,
	ORDER TanRifle4 GOTO StationUR INMODE IGNORE THEN ATMODE DEFEND,
	SETAICONTROL TanRifle3,
	ORDER TanRifle3 GOTO StationUL INMODE IGNORE THEN ATMODE DEFEND

IF ALLOF on_Region4 vison_GreenSarge THEN
	SETAICONTROL TanRifle3,
	ORDER TanRifle3 GOTO Cover21 INMODE IGNORE THEN ATMODE STANDFIRE
IF off_Region4 THEN
	SETAICONTROL TanRifle3,
	ORDER TanRifle3 GOTO StationUL INMODE IGNORE THEN ATMODE DEFEND

IF ALLOF on_Region5 vison_GreenSarge THEN
	SETAICONTROL TanRifle3,
	ORDER TanRifle3 GOTO Cover20 INMODE IGNORE THEN ATMODE STANDFIRE
IF off_Region5 THEN
	SETAICONTROL TanRifle3,
	ORDER TanRifle3 GOTO StationUL INMODE IGNORE THEN ATMODE DEFEND

IF ALLOF on_Region6 vison_GreenSarge BUTNOT VerdeEnCasa THEN
	TRIGGER VerdeEnCasa,
	SETAICONTROL TanRifle2,
	ORDER TanRifle2 GOTO GreenSarge INMODE ATTACK THEN ATMODE DEFEND,
	SETAICONTROL TanRifle3,
	ORDER TanRifle3 GOTO GreenSarge INMODE ATTACK THEN ATMODE DEFEND,
	SETAICONTROL TanRifle4,
	ORDER TanRifle4 GOTO GreenSarge INMODE ATTACK THEN ATMODE DEFEND
IF ALLOF on_Region6 vison_GreenSarge THEN
	SETAICONTROL TanRifle1,
	ORDER TanRifle1 FOLLOW GreenSarge INMODE DEFEND
IF ALLOF on_Region6 vison_GreenSarge 6Out BUTNOT killed_TanRifle6 THEN
	SETAICONTROL TanRifle6,
	ORDER TanRifle6 GOTO GreenSarge INMODE ATTACK THEN ATMODE DEFEND
IF on_Region6 BUTNOT 6Out THEN
	DEPLOY TanRifle6 ATMODE DEFEND,
	SETAICONTROL TanRifle6,
	ORDER TanRifle6 GOTO GreenSarge INMODE ATTACK THEN ATMODE DEFEND,
	TRIGGER 6Out
IF INORDER VerdeEnCasa visoff_GreenSarge THEN
	SETAICONTROL TanRifle1,
	ORDER TanRifle1 GOTO StationLR INMODE IGNORE THEN ATMODE DEFEND,
	SETAICONTROL TanRifle2,
	ORDER TanRifle2 GOTO StationLL INMODE IGNORE THEN ATMODE DEFEND,
	SETAICONTROL TanRifle3,
	ORDER TanRifle3 GOTO StationUL INMODE IGNORE THEN ATMODE DEFEND,
	SETAICONTROL TanRifle4,
	ORDER TanRifle4 GOTO StationUR INMODE IGNORE THEN ATMODE DEFEND,
	SETAICONTROL TanRifle6,
	ORDER TanRifle6 GOTO Cover16 INMODE IGNORE THEN ATMODE DEFEND








//EASTERN FRONT
IF Frontline THEN
	DEPLOY GreenRifle1 ATMODE ATTACK,
	SETAICONTROL GreenRifle1,
	ORDER GreenRifle1 GOTO (3565, 1333) INMODE ATTACK THEN ATMODE ATTACK,
	DEPLOY GreenRifle2 ATMODE ATTACK,
	SETAICONTROL GreenRifle2,
	ORDER GreenRifle2 GOTO (3460, 755) INMODE ATTACK THEN ATMODE ATTACK,
	DEPLOY GreenRifle3 ATMODE ATTACK,
	SETAICONTROL GreenRifle3,
	ORDER GreenRifle3 GOTO (3460, 755) INMODE ATTACK THEN ATMODE ATTACK,
	SETAICONTROL UGreenTank1,
	ORDER UGreenTank1 GOTO (3661, 482) INMODE IGNORE THEN ATMODE DEFEND,
	SETAICONTROL UTanTank1,
	ORDER UTanTank1 GOTO (3517, 805) INMODE IGNORE THEN ATMODE DEFEND

//FRONTLINE resurrections
IF killed_UTanTank1 THEN
	RESURRECT UTanTank1,
	SETAICONTROL UTanTank1,
	ORDER UTanTank1 GOTO (3517, 805) INMODE IGNORE THEN ATMODE DEFEND

IF killed_UGreenTank1 THEN
	RESURRECT UGreenTank1,
	SETAICONTROL UGreenTank1,
	ORDER UGreenTank1 GOTO (3661, 482) INMODE ATTACK THEN ATMODE DEFEND
IF killed_GreenRifle1 THEN
	RESURRECT GreenRifle1 (3946, 1225),
	SETAICONTROL GreenRifle1,
	ORDER GreenRifle1 GOTO (3611, 1383) INMODE ATTACK THEN ATMODE DEFEND
IF killed_GreenRifle2 THEN
	RESURRECT GreenRifle2 (3946, 1225),
	SETAICONTROL GreenRifle2,
	ORDER GreenRifle2 GOTO (3470, 750) INMODE ATTACK THEN ATMODE DEFEND
IF killed_GreenRifle3 THEN
	RESURRECT GreenRifle3 (3946, 1225),
	SETAICONTROL GreenRifle3,
	ORDER GreenRifle3 GOTO (3466, 725) INMODE ATTACK THEN ATMODE DEFEND
IF killed_TanRifle7 THEN
	RESURRECT TanRifle7 (3320, 160),
	SETAICONTROL TanRifle7,
	ORDER TanRifle7 GOTO (3421, 535) INMODE RETURNFIRE THEN ATMODE DEFEND
IF killed_TanRifle24 THEN
	RESURRECT TanRifle24 (3320, 160),
	SETAICONTROL TanRifle24,
	ORDER TanRifle24 GOTO (3611, 1383) INMODE RETURNFIRE THEN ATMODE DEFEND
IF killed_TanRifle8 THEN
	RESURRECT TanRifle8 (3320, 160),
	SETAICONTROL TanRifle8,
	ORDER TanRifle8 GOTO (3466, 725) INMODE RETURNFIRE THEN ATMODE DEFEND
IF killed_TanRifle25 THEN
	RESURRECT TanRifle25 (3320, 160),
	SETAICONTROL TanRifle25,
	ORDER TanRifle25 GOTO (3441, 1100) INMODE RETURNFIRE THEN ATMODE DEFEND
IF killed_TanGrenadier1 THEN
	RESURRECT TanGrenadier1 (3320, 160),
	SETAICONTROL TanGrenadier1,
	ORDER TanGrenadier1 GOTO (3470, 749) INMODE RETURNFIRE THEN ATMODE DEFEND
IF killed_TanRifle34 THEN
	RESURRECT TanRifle34 (3320, 160),
	SETAICONTROL TanRifle34,
	ORDER TanRifle34 GOTO (3471, 1435) INMODE RETURNFIRE THEN ATMODE DEFEND

//delay Green grenadier and Flamethrower guy
IF TIMEABSOLUTE 15 THEN
	DEPLOY GreenGrenadier1 ATMODE ATTACK,
	SETAICONTROL GreenGrenadier1,
	ORDER GreenGrenadier1 GOTO (3441, 1100) INMODE ATTACK THEN ATMODE ATTACK
IF killed_GreenGrenadier1 THEN
	TRIGGERDELAY 30 ResGren
IF ResGren THEN
	RESURRECT GreenGrenadier1 (3900, 1025),
	SETAICONTROL GreenGrenadier1,
	ORDER GreenGrenadier1 GOTO (3441, 1100) INMODE ATTACK THEN ATMODE ATTACK

IF TIMEABSOLUTE 45 THEN
	DEPLOY GreenFlamer1 ATMODE ATTACK,
	SETAICONTROL GreenFlamer1,
	ORDER GreenFlamer1 GOTO (3460, 755) INMODE RETURNFIRE THEN ATMODE DEFEND
IF killed_GreenFlamer1 THEN
	TRIGGERDELAY 60 ResFlam
IF ResFlam THEN
	RESURRECT GreenFlamer1 (3900, 660),
	SETAICONTROL GreenFlamer1,
	ORDER GreenFlamer1 GOTO (3460, 755) INMODE RETURNFIRE THEN ATMODE DEFEND

//item drops
IF killed_TanRifle13 THEN
	REMOVEOBJECT Meds32,
	PLACEOBJECT Meds32 TanRifle13
IF killed_TanRifle27 THEN
	REMOVEOBJECT Meds8,
	PLACEOBJECT Meds8 TanRifle27
IF killed_TanRifle14 THEN
	REMOVEOBJECT Meds9,
	PLACEOBJECT Meds9 TanRifle14
IF killed_MagiCrate1 THEN
	REMOVEOBJECT Grenades1,
	PLACEOBJECT Grenades1 MagiCrate1
IF killed_MagiCrate2 THEN
	REMOVEOBJECT MedPack1,
	PLACEOBJECT MedPack1 MagiCrate2
IF killed_MagiCrate3 THEN
	REMOVEOBJECT Flam3,
	PLACEOBJECT Flam3 MagiCrate3
IF killed_RobertZalot THEN
	REMOVEOBJECT Grenades2,
	PLACEOBJECT Grenades2 BonusCrate1
IF killed_RichardsBox THEN
	REMOVEOBJECT RichardHicks,
	PLACEOBJECT RichardHicks RichardsBox
IF killed_NicksBox THEN
	REMOVEOBJECT NickEarl,
	PLACEOBJECT NickEarl NicksBox 
IF killed_BonusCrate2 THEN
	REMOVEOBJECT Flam007,
	PLACEOBJECT Flam007 BonusCrate2
IF killed_BonusCrate1 THEN
	REMOVEOBJECT AlvinMuolic,
	PLACEOBJECT AlvinMuolic BonusCrate1 

